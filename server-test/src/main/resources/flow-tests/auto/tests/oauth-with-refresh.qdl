#! /usr/bin/env -S qdl-run

/*
   Tests a client with no configuration. This is as basic as it gets.
*/
debugger(1);
params. ≔ {'sub':'jeff'};
test_id:= 'oauth.conf.basic';
clc#init($$OA4MP_CLC_INI, test_id);

initialization := size(args())==1⇒args().0:import(load('test#/auto/utils/initialization.mdl'));
//checks:=initialization#checks();
ini.:=initialization#get_client_ini(test_id);
server_defaults.:=initialization# server_defaults();
//cc.:=initialization#get_client_config(test_id);

tokens. := clc#rfc7523(params.);
⊨ tokens.'access_token'.'lifetime' == 1000*server_defaults.'at_lifetime' : 'wrong access token lifetime';
⊨ ∃tokens.'refresh_token' : 'did not got a refresh token.';
⊨ tokens.'refresh_token'.'lifetime' == 1000*server_defaults.'rt_lifetime' : 'wrong refresh token lifetime';
⊨ size(clc#claims()) == 0 : ' got claims where none should be';

// now redo requesting specific lifetimes
params.'at_lifetime' := '1009 sec';
params.'rt_lifetime' := '6 hr';
tokens. := clc#rfc7523(params.);
⊨ tokens.'access_token'.'lifetime' == 1009000 : 'wrong access token lifetime';
⊨ tokens.'refresh_token'.'lifetime' == 6*3600*1000 : 'wrong refresh token lifetime';

tokens. := clc#refresh();
⊨ tokens.'access_token'.'lifetime' == 1009000 : 'wrong access token lifetime';
⊨ tokens.'refresh_token'.'lifetime' == 6*3600*1000 : 'wrong refresh token lifetime';
⊨ size(clc#claims()) == 0 : ' got claims where none should be';
say('ok: OAuth confidential, with refresh tokens, no claims');