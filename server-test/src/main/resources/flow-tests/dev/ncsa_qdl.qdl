#! /usr/bin/env -S qdl-run

/*
   Tests the client with no configuration

*/
path := os_env('NCSA_DEV_INPUT')+'/oa4mp/server-test/src/main/resources/flow-tests';
if[path∉script_path()][script_path(path ~ script_path());];
cfg.'id'  := 'dev:/test/ncsa_qdl';
cfg.'idp'  := 'NCSA';
cfg.'file' := $$OA4MP_CLC_XML_INI;
cfg.'description' :=  'Test standard NCSA claims processing using QDL. Critical regression test.';
cfg.'flow_type' := 'uri'; // Note to use df you need to set param for the token endpoint

rc := script_load('driver.qdl', cfg.);
if[rc!='ok'][return(rc);];

tokens. := clc#access();
claims. := clc#claims();
⊨ ∄claims.'memberof' : 'memberof claim not removed';
⊨ ∄claims.'cn' : 'cn claim not removed';
⊨ ∄claims.'mail' : 'mail claim not removed';
⊨ ∃claims.'isMemberOf' : 'isMemberOf claim is missing';
⊨ ∃claims.'name' : 'name claim is missing';
⊨ ∃claims.'email' : 'email claim is missing';
⊨ claims.'aud' == cfg.'id' : 'wrong audience';
groups. := claims.'isMemberOf';
⊨ groups. << List : 'is member of claim is not a group';
⊨ groups.0 << String : 'is member of claim is not a flat list';
⊨ tokens.'access_token'.'lifetime' == 1009000 : 'wrong access token lifetime';
⊨ tokens.'refresh_token'.'lifetime' == 1000000 : 'wrong refresh token lifetime';

tokens. := clc#refresh();
claims. := clc#claims();
⊨ claims.'aud' == cfg.'id' : 'wrong audience';
groups. := claims.'isMemberOf';
⊨ groups. << List : 'is member of claim is not a group';
⊨ groups.0 << String : 'is member of claim is not a list';
⊨ tokens.'access_token'.'lifetime' == 1009000 : 'wrong access token lifetime';
⊨ tokens.'refresh_token'.'lifetime' == 1000000 : 'wrong refresh token lifetime';

say('ok');

