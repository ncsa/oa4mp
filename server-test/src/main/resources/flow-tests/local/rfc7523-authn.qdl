#! /usr/bin/env -S qdl-run

/*
   Tests running a sequence of QDL scripts on the server.
   Note that this also tests extended attribute support, which is
   enabled in the CLI (using the ea_support command). This should return
   a string of two values that can be parsed. 
*/

cfg.'id'  := 'ashigaru:command.line2';
cfg.'idp'  := 'ANY';
cfg.'file' := $$OA4MP_CLC_XML_INI;
cfg.'description' :=  'RFC7523 authentication test, then chained QDL scripts in a single handler.';
cfg.'flow_type' := 'uri'; // Note to use df you need to set param for the token endpoint
params.'a' := {'org.oa4mp:/tokens/access/lifetime':1000000,'org.oa4mp:/roles':'admin,all-user'};
rc := script_load('driver.qdl', cfg., params.);
if[rc!='ok'][return(rc);];

define[test(tokens., claims., phase)]
  body[
        ⊨ tokens.'access_token'.'lifetime' ≡ 900000 : 'wrong access token lifetime';
        ⊨ tokens.'refresh_token'.'lifetime' ≡ 1000000000 : 'wrong refresh token lifetime';
        ⊨ claims.'seq0' ≡ 636 : 'wrong seq 0 value';
        ⊨ claims.'roles'.0 ≡ 'admin,all-user' : 'wrong extended attribute value';
        ⊨ claims.'seq0_phase' ≡ phase : 'wrong seq0_phase value';
        ⊨ claims.'seq1' ≡ 'is_member_of' : 'wrong seq 1 value';
        ⊨ claims.'seq2' ≡ 'seq 2 passed ' + phase : 'wrong seq 2 value';
  ];
tokens. := clc#access();
claims. := clc#claims();
test(tokens., claims., 'post_token');

tokens. := clc#refresh();
claims. := clc#claims();
test(tokens., claims., 'post_refresh');

// No exchange at this point since that does nothing to the claims.
say('RFC7523 test of multiple QDL scripts: ok');