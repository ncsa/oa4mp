/*
   This is for linking identities. It assumes that the voPersonExternalID is already in
   LDAP and will do a search there for it. If found, then it will do a second LDAP query
   for the user's NCSA-specific information.
   Author: Jeff Gaynor
   Created: 2020-10-31T12:39:30.422Z
   Last modified: 2021-02-17T12:30:30.540Z

   The client configuration:

   {"tokens": {"identity": {
    "qdl":  {
     "load": "lsst/linking.qdl",
     "xmd": {"exec_phase":   [
      "pre_auth",
      "post_token"
     ]}
    },
    "type": "identity"
   }}}

  tokens{
     identity{
        type=identity
        qdl{
            load="lsst/linking.qdl"
            xmd={exec_phase=["pre_auth","post_token"]}
          }// end qdl
        } //end identity token
  } //end tokens

    tokens{
     identity{
        type=identity
        qdl{
            load="lsst/linking.qdl"
            xmd={exec_phase="post_token"}
          }// end qdl
        } //end identity token
        access{
           type=wlcg
           issuer="https:cilogon.org"
           audience="https://wlcg.cern.ch/jwt/v1/any"
           lifetime=3600000
           qdl{
              load="fnal/fnal-at.qdl"
              xmd={exec_phase=["post_token","post_refresh","post_exchange"]}
              args=["USER_ID", "PASSWORD"]
           } //end QDL
        } // end access token
      } //end tokens

*/

// Linking for LSST (CIL-523): Do two stage lookup if the user did not use the NCSA LDAP
// to link their external account.

script_load('utils/init.qdl');

v := null;
switch[
  if[claims.idp == idp.github][v:=claims.oidc+'@github.com';];
  if[claims.idp == idp.google][v:=claims.oidc+'@accounts.google.com';];
  if[claims.idp == idp.orcid][v:=replace(claims.oidc,'http://', 'https://');];
  if[is_defined(claims.eppn)][v:=claims.eppn;];
  if[is_defined(claims.eptid)][v:=claims.eptid;];
];
if[
     v != null
   ][
// go look up voPersonExternalID  first in LDAP
                    cfg. := new_template('ncsa');
                    cfg. := create_source(cfg.);
          cfg.claim_name := 'uid';
           cfg.ldap_name := 'voPersonExternalID';
         cfg.search_base := 'ou=People,dc=ncsa,dc=illinois,dc=edu';
  cfg.search_attributes. := ['uid'];
                cfg.type := 'ldap'; // Set the type to LDAP or the handler will on search by uid
say('***QDL: cfg. ==' + to_string(cfg.));
                      c. := get_claims(cfg., v);
say('***QDL: got VOP claims:' + to_string(c.));
   if[is_defined(c.uid)][claims.uid := c.uid;];
]else[
   // no recognized IDP at this point -- they used something we don't honor.
   if[
       claims.idp != idp.ncsa
     ][
        return();
     ]else[
        claims.uid := head(claims.eppn, '@');
     ];
];
// now add any of these claim we got to the result.
say('***QDL: getting claims');
             cfg. := new_template('ncsa');
             cfg. := create_source(cfg.);
          claims. := claims. ~ get_claims(cfg., claims.uid);
say('***QDL: Returning claims:' + to_string(claims.));
