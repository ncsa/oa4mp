/*
   Basic script for testing. This will take xa. parameters and set
   token claims based on them. This allows for having a client
   various claims and test that they are handled correctly, effectively
   letting the client have many possible configurations. The aim
   is diversity in testing without having to have a ton of specialized
   clients.

   The format of an entry is
   /token/value/claim := value

   E.g.
   /idt/value/iss := 'https:/oa4mp.org'

   tokens are idt, at or rt.

*/


 if[size(xas.) == 0][return();];

say('xas.=' + input_form(xas.));
say('exec_phase='+exec_phase);
oa4mp.:=xas.'org.oa4mp';
z.:=null;
// This uses the fact that a list is an index, viz.,
// z.['a','b'] ==z.'a'.'b'
// so tokenizing the keys returns the index we want.
while[k∋oa4mp.][z.tokenize(k,'/'):=oa4mp.k;]; // convert to a stem
say('z:\n' + print(z.));

define[set_claims(token., key, values.)][
  say('set_claims: values.\n' + print(values.));
  λ. := values\key\'value'; // a stem of {claim:value} pairs
  while[k∋λ.][λ.k := (size(λ.k)==1)?(λ.k.0):λ.k;];
  say('set_claims: for ' + key + ' λ.\n' + print(λ.));
  while[k∋λ.][token.k := λ.k;]; // set the claims en masse
]; // end define set_claims

// one last bit of cleanup. By default, the system only returns
// arrays of values, so convert single values to single values.

if[exec_phase∈['post_auth','post_user_info']][
   if[∃claims.][
     say('claims:\n' + print(claims.));
     set_claims(claims., 'idt', z.);
  ];
];

if[exec_phase∈['post_token', 'post_refresh', 'post_exchange']][
   // might not be an access token depending on the configuration.
   if[∃access_token.][
     say('at:\n' + print(access_token.));
     set_claims(access_token., 'at', z.);
   ];
   // might not be a refresh token depending on the configuration.
   if[∃refresh_token.][
     say('rt:\n' + print(refresh_token.));
     set_claims(refresh_token., 'rt', z.);
   ];
];

