/*
   Default NCSA LDAP claim source. This will retrieve all group membership information.
   This will do nothing unless the IDP is correct.  Note if you reference this, you must
   have both a pre_auth and post_token phase: this adds itself as a claim source for
   the usual claims system.

   updated: 2021-06-03T14:58:43.439Z


  tokens{
    identity{
    type=identity
    qdl{
        load="ncsa/ncsa-default.qdl"
        xmd={exec_phase=["pre_auth","post_token","post_refresh"]}
        args=["true"] // true if the member of claim is just a list. Default is false.
      }// end qdl
    } //end identity token
  } //end tokens

*/
debugger(1);

script_load('utils/init.qdl');

(∄claims.'idp' ∨ claims.'idp' ≠ idp.'ncsa') ⇒ return(); // never process anything unless IDP is right.

if[
     exec_phase ≡ 'pre_auth'
]then[
     cc ≔ j_load($$OA4MP_PATH + '.util.claims');
     cfg. ≔ cc#new_template('ncsa');
     debugger('*** NCSA claims=' + print(claims.));
     claims.'uid' ≔ head(claims.'eppn', '@'); // set search key
     claim_sources. ≔ claim_sources. ~ [cc#create_source(cfg.)]; // add it to any claims
     debugger('*** NCSA updated claims #1 =' + print(claims.));
     return();
]; //end if

 (exec_phase ∉ ['post_token', 'post_refresh', 'post_user_info']) ⇒ return();
 0≡script_args() ⇒ return(); // do not process claims

 (!is_defined(claims.'isMemberOf')) ⇒ return();
 if[to_boolean(script_args(0))][claims.'isMemberOf' := claims.isMemberOf\*\name;];
 return();
